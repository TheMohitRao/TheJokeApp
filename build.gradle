// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.7.20'
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.15'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext.coreDependencies = {dependencyHandler->
    delegate = dependencyHandler

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.greenrobot:eventbus:3.3.1'
}

ext.koinDependencies = {dependencyHandler->
    delegate = dependencyHandler

    //koin
    runtimeOnly 'io.insert-koin:koin-core:3.3.3'
    implementation 'io.insert-koin:koin-androidx-compose:3.4.2'
    implementation 'io.insert-koin:koin-android:3.3.3'
    implementation 'io.insert-koin:koin-androidx-workmanager:3.3.3'
}

ext.uiDependencies = {dependencyHandler->
    delegate = dependencyHandler

    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.airbnb.android:lottie:5.2.0'
}

ext.liveDataDependencies = {dependencyHandler->
    delegate = dependencyHandler

    def lifecycle_version = '2.5.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
}

ext.rxJavaDependencies = {dependencyHandler->
    delegate = dependencyHandler

    //rxjava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
}

ext.testingDependencies = {dependencyHandler->
    delegate = dependencyHandler

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
ext.dbDependencies = {dependencyHandler->
    delegate = dependencyHandler

    def room_version = '2.5.0'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt 'android.arch.persistence.room:compiler:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
}